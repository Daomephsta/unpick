plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

archivesBaseName='unpick'
group = 'io.github.weavemc'
version = '2.0.1+build.unknown'
def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
	version = "2.0.1+build.${ENV.BUILD_NUMBER}"
}

targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    api 'org.ow2.asm:asm:7.0'
	api 'org.ow2.asm:asm-tree:7.0'
	implementation 'org.ow2.asm:asm-commons:7.0'
	implementation 'org.ow2.asm:asm-util:7.0'
	implementation project(':unpick-format-utils')

    // Use JUnit test framework
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.+'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
	repositories {
        if (project.hasProperty('weavemc_pass')) {
			println 'Password found. Remote maven available.'
            maven {
                url = 'http://deploy.modmuss50.me/'
                credentials {
                    username = 'weavemc'
                    password = project.getProperty('weavemc_pass')
                }
            }
        }
    }
    publications {
    	binary(MavenPublication) {
            from components.java
    	}
    	source(MavenPublication) {
            artifact sourcesJar
    	}
    	javadoc(MavenPublication) {
            artifact javadocJar
    	}
	}
}
